apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

Properties projectProperties = new Properties()
projectProperties.load(rootProject.file('gradle.properties').newDataInputStream())

android {
    compileSdkVersion 33

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 33
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lint {
        abortOnError false
    }
    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = group = projectProperties.getProperty("bintray.groupId")
                artifactId = projectProperties.getProperty("bintray.artifact")
                version = projectProperties.getProperty("bintray.libraryVersion")

                pom {
                    name = projectProperties.getProperty("bintray.libraryName")
                    description = projectProperties.getProperty("bintray.libraryDescription")
                    url = projectProperties.getProperty("bintray.siteUrl")
                    licenses {
                        license {
                            name = projectProperties.getProperty("bintray.licenseName")
                            url = projectProperties.getProperty("bintray.licenseUrl")
                        }
                    }
                    developers {
                        developer {
                            id = projectProperties.getProperty("bintray.developerId")
                            name = projectProperties.getProperty("bintray.developerName")
                            email = projectProperties.getProperty("bintray.developerEmail")
                        }
                    }
                    scm {
                        connection = projectProperties.getProperty("bintray.gitUrl")
                        developerConnection = projectProperties.getProperty("bintray.gitUrl")
                        url = projectProperties.getProperty("bintray.siteUrl")
                    }
                }
            }
        }
    }
}

dependencies {
    api fileTree(include: ['*.jar'], dir: 'libs')
    api 'androidx.legacy:legacy-support-v4:+'
    api 'androidx.exifinterface:exifinterface:+'
    api 'androidx.camera:camera-core:+'
    api 'androidx.camera:camera-camera2:+'
    api 'androidx.camera:camera-lifecycle:+'
    testImplementation 'org.robolectric:robolectric:+'
    testImplementation 'org.mockito:mockito-core:+'
    testImplementation 'junit:junit:+'
}