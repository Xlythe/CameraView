plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.compose' version '2.1.20'
}

Properties projectProperties = new Properties()
projectProperties.load(rootProject.file('gradle.properties').newDataInputStream())

Properties localProperties = new Properties()
localProperties.load(project.rootProject.file('local.properties').newDataInputStream())

android {
    compileSdkVersion 36

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 36
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }

    buildFeatures {
        compose = true
    }

    lint {
        abortOnError false
    }
    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
    namespace 'com.xlythe.view.camera'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = group = projectProperties.getProperty("bintray.groupId")
                artifactId = projectProperties.getProperty("bintray.artifact")
                version = projectProperties.getProperty("bintray.libraryVersion")

                pom {
                    name = projectProperties.getProperty("bintray.libraryName")
                    description = projectProperties.getProperty("bintray.libraryDescription")
                    url = projectProperties.getProperty("bintray.siteUrl")
                    licenses {
                        license {
                            name = projectProperties.getProperty("bintray.licenseName")
                            url = projectProperties.getProperty("bintray.licenseUrl")
                        }
                    }
                    developers {
                        developer {
                            id = projectProperties.getProperty("bintray.developerId")
                            name = projectProperties.getProperty("bintray.developerName")
                            email = projectProperties.getProperty("bintray.developerEmail")
                        }
                    }
                    scm {
                        connection = projectProperties.getProperty("bintray.gitUrl")
                        developerConnection = projectProperties.getProperty("bintray.gitUrl")
                        url = projectProperties.getProperty("bintray.siteUrl")
                    }
                }
            }
        }
        repositories {
            maven {
                name = "MavenCentral"
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username = localProperties.getProperty("bintray.username")
                    password = localProperties.getProperty("bintray.password")
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            localProperties.getProperty("signing.keyId"),
            localProperties.getProperty("signing.key"),
            localProperties.getProperty("signing.password"),
    )
    sign publishing.publications
}

dependencies {
    api fileTree(include: ['*.jar'], dir: 'libs')
    api 'androidx.legacy:legacy-support-v4:[1.0.0,)'
    api 'androidx.exifinterface:exifinterface:[1.4.1,)'
    api 'androidx.camera:camera-core:[1.4.2,)'
    api 'androidx.camera:camera-camera2:[1.4.2,)'
    api 'androidx.camera:camera-lifecycle:[1.4.2,)'
    api 'androidx.camera:camera-video:[1.4.2,)'
    api 'com.google.guava:guava:[31.1-android,)'
    api 'com.google.mlkit:barcode-scanning:[17.3.0,)'
    api 'androidx.activity:activity-compose:[1.10.1,)'
    api 'androidx.lifecycle:lifecycle-runtime-ktx:[2.8.7,)'
    testImplementation 'org.robolectric:robolectric:+'
    testImplementation 'org.mockito:mockito-core:+'
    testImplementation 'junit:junit:+'
    constraints {
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.0") {
            because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
        }
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0") {
            because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
        }
    }
}